evaluationDependsOn(':server')

apply plugin: 'com.android.application'

android {
    namespace 'org.client.scrcpy'
    flavorDimensions = ["default"]

    compileSdkVersion 31
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 9
        versionName "1.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "scrcpy")
    }
    signingConfigs {
        release {
            storeFile file("scrcpy.jks")

            def properties = new Properties()
            file("../local.properties").withInputStream { stream ->
                properties.load(stream)
            }

            storePassword System.getenv("SIGNING_STORE_PASSWORD") ?: properties.getProperty("SIGNING_STORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS") ?: properties.getProperty("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD") ?: properties.getProperty("SIGNING_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        scrcpy {
            applicationId "org.client.scrcpy"
        }
    }


    tasks.whenTaskAdded { task ->
        def buildType = project.hasProperty('buildType') ? project.buildType : 'Debug'

        task.dependsOn ":server:assemble${buildType}"
        task.dependsOn ":server:copyServer"
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
